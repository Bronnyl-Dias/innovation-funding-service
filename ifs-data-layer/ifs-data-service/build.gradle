

def targetBuild = hasProperty('profile') ? profile : 'docker'
apply from: targetBuild + "-build.gradle"

apply plugin: 'base'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'org.asciidoctor.convert'

apply from: "db-reset.gradle"
apply from: rootProject.file("openshift-build.gradle")
apply from: rootProject.file("prepare-docker.gradle")

task('copyFreemarkerResources', type: Copy) {
//Copy freemarker resources to build path to support running as a jar.
    from 'src/main/java/org/innovateuk/ifs/notifications/templates'
    into 'build/classes/main/org/innovateuk/ifs/notifications/templates'
    include '**/*'
}

compileJava.dependsOn copyFreemarkerResources


configurations {
    providedRuntime
}

project.version = dataServiceVersion
tagDocker.tag = dataServiceVersion
osStageRemote.tag = dataServiceVersion
tagDockerLocal.tag = dataServiceVersion
osStageLocal.tag = dataServiceVersion

ext.dockerDir = "$project.parent.projectDir/docker/"

buildDocker.dependsOn prepareDockerInput
buildDocker.dependsOn ":docker:app-base-image:buildDocker"
buildDocker.tag = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"
buildDocker.inputDir = project.file("$project.buildDir/docker")
tagDocker.imageId = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"

ext.idpRestKey = project.properties['idp.rest.key'] ?: "1234567890"
ext.companyHouseKey = ext.get('ifs.company-house.key')

sourceSets {
    client {
        java {
            srcDir 'src/main/java'
        }
        main {
            ext.originalJavaSrcDirs = java.srcDirs
            java.srcDir "${generatedMapperSourcesDir}"
        }
    }
}

bootRun {
    addResources = false
}

ext {
    snippetsDir = file('build/generated-snippets')
}

// Spring Boot 1.3 uses Spring REST Docs 1.0. To use 1.1, override the version
ext['spring-restdocs.version'] = '1.1.2.RELEASE'

jar {
    baseName = 'ifs-data-service'
}

configurations {
    mapstruct
}

dependencies {
    compile project(":ifs-data-layer:data-service-commons")
    compile project(":ifs-data-layer:data-service-file")

    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.hibernate:hibernate-core:5.2.7.Final"
    compile "org.springframework.security:spring-security-crypto"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "mysql:mysql-connector-java"
    compile "org.springframework:spring-web"
    compile "com.jayway.jsonpath:json-path-assert:2.0.0"
    compile "com.jayway.jsonpath:json-path"
    compile "org.springframework.statemachine:spring-statemachine-core:1.1.0.RELEASE"
    compile "org.springframework.statemachine:spring-statemachine-recipes-common:1.1.0.RELEASE"
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'commons-codec:commons-codec:1.10'
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile "org.flywaydb:flyway-core:4.1.2"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "org.mapstruct:mapstruct:1.0.0.Final"
    mapstruct 'org.mapstruct:mapstruct-processor:1.0.0.Final'
    compile 'org.freemarker:freemarker:2.3.25-incubating'
    compile "org.apache.poi:poi:3.14"
    compile "org.apache.poi:poi-ooxml:3.14"

    compile 'javax.mail:mail:1.4.7'
    compile "org.apache.httpcomponents:httpasyncclient"
    compile "com.google.guava:guava:22.0"
    compile "commons-io:commons-io:2.5"
    compile 'org.jsoup:jsoup:1.10.3'
    compile "net.sf.opencsv:opencsv:2.3"
    compile "org.xhtmlrenderer:flying-saucer-core:9.1.6"
    compile "org.xhtmlrenderer:flying-saucer-pdf:9.1.6"

    testCompile project(':ifs-resources').sourceSets.test.output
    testCompile project(":ifs-commons").sourceSets.test.output
    testCompile project(":ifs-data-layer:data-service-commons").sourceSets.test.output
    testCompile project(":ifs-data-layer:data-service-file").sourceSets.test.output

    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:1.1.2.RELEASE"
    testCompile "com.openpojo:openpojo:0.8.4"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.assertj:assertj-core:3.9.0'
}

test {
    outputs.dir snippetsDir
    if (project.hasProperty('excludeTests')) {
        exclude "${project.excludeTests}"
    }

    if (project.hasProperty('testGroups')) {
        systemProperty 'testGroups', project.getProperty('testGroups')
    }
    finalizedBy asciidoctor
}




asciidoctor {
    dependsOn test
    attributes 'snippets': snippetsDir
    backends 'html5'
    inputs.dir snippetsDir
    outputs.dir file("static/docs")

    doLast {
        copy {
            from "build/asciidoc/html5"
            into 'static/docs'
        }
    }

}

task generateMainMapperClasses(type: JavaCompile) {
    ext.aptDumpDir = file("build/tmp/apt/mapstruct")
    destinationDir = aptDumpDir

    classpath = compileJava.classpath + configurations.mapstruct
    source = sourceSets.main.originalJavaSrcDirs
    ext.sourceDestDir = file generatedMapperSourcesDir

    options.define(
            compilerArgs: [
                    "-nowarn",
                    "-proc:only",
                    "-encoding", "UTF-8",
                    "-processor", "org.mapstruct.ap.MappingProcessor",
                    "-s", sourceDestDir.absolutePath,
                    "-source", 1.8,
                    "-target", 1.8,
            ]
    );

    inputs.dir source
    outputs.dir generatedMapperSourcesDir
    doFirst {
        sourceDestDir.mkdirs()
    }
    doLast {
        aptDumpDir.delete()
    }
}

task syncShib(type:Exec) {
    workingDir '../'
    commandLine 'docker-compose', 'exec', '-T', 'ldap', '/usr/local/bin/ldap-sync-from-ifs-db.sh'
}

task initDB() {
    flywayMigrate.mustRunAfter flywayClean

    dependsOn flywayClean
    dependsOn flywayMigrate
}

compileJava.dependsOn generateMainMapperClasses
flywayMigrate.dependsOn processResources
flywayClean.dependsOn processResources




def targetBuild = hasProperty('profile') ? profile : 'docker';
apply from: targetBuild + "-build.gradle";
apply from: "fractal.gradle";

subprojects {


    apply from: rootProject.file("java-spring-boot.gradle")
    // ifs-web-core is not a docker project.
    if (it.name != 'ifs-web-core') {

        apply from: rootProject.file("openshift-build.gradle")
        apply from: rootProject.file("new-relic.gradle")


        /**
         * This task creates a `docker` directory in the $BUILD/docker` directory of the project,
         * where it will write all the necessary files to build a docker container.
         * In other words, after this task is executed, one could manually run `docker build .`
         * in the output directory.
         */
        task prepareDockerInput {
            dependsOn getNewRelicAgent
            dependsOn bootRepackage
            def inputDir = project.file("$project.buildDir/docker")

            doFirst {
                copy {
                    from '../docker/'
                    into inputDir
                    include 'Dockerfile-template'
                    rename 'Dockerfile-template', 'Dockerfile'
                    filter {
                        it.replaceAll('@version@', project.properties['version']).replaceAll('@app_name@', jar.baseName)
                    }
                }
                copy {
                    from bootRepackage.outputs
                    into inputDir
                }
                copy {
                    from file("${rootProject.projectDir}/setup-files/scripts/docker/coscale-monitoring.sh")
                    into inputDir
                }
                copy {
                    from file("${rootProject.projectDir}/setup-files/scripts/docker/newrelic.jar")
                    from file("${rootProject.projectDir}/setup-files/scripts/docker/newrelic.yml")
                    into inputDir
                }
            }
        }

        buildDocker.dependsOn ":docker:app-base-image:buildDocker"
        buildDocker.dependsOn prepareDockerInput
        buildDocker.tag = "innovateuk/"+project.name.replaceAll(/ifs-/,"")+":latest"
        buildDocker.inputDir = project.file("$project.buildDir/docker")
        tagDocker.imageId="innovateuk/"+project.name.replaceAll(/ifs-/,"")+":latest"

        // Workaround for https://github.com/spring-projects/spring-boot/issues/8308
        // Can reinstate excludeDevtools in the bootRepackage task in next release
        springBoot {
            excludeDevtools = !(project.hasProperty('cloud') && cloud == 'development');
        }

        bootRepackage {
            mainClass = 'org.innovateuk.ifs.Application'
            classifier = "autostart"
        //    excludeDevtools = !(project.hasProperty('cloud') && cloud == 'development');

            // By explicitly adding the inputs and outputs to this task, we can utilize the `incremental build' feature.
            inputs.files(jar.outputs)
            outputs.file file(jar.archiveName.toString().replace(".jar", "-autostart.jar"))
        }
    }
}
apply from: "documentation.gradle"


endpointDocumentation.dependsOn "ifs-application-service:test"
endpointDocumentation.dependsOn "ifs-assessment-service:test"
endpointDocumentation.dependsOn "ifs-competition-mgt-service:test"
endpointDocumentation.dependsOn "ifs-project-setup-mgt-service:test"
endpointDocumentation.dependsOn "ifs-project-setup-service:test"
endpointDocumentation.dependsOn "ifs-front-door-service:test"

